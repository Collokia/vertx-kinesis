description "Collokia's Vertx-Kinesis Integration"

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

configurations {
    annotationProcessor
}

ext {
    generatedSrcDir = "$projectDir/src/generated"
}

sourceSets {
    main {
        groovy {
            srcDir "$generatedSrcDir/groovy"
        }
        resources {
            srcDir "$generatedSrcDir/resources"
        }
    }
}

dependencies {
    compile "io.vertx:vertx-core:$version_vertx"
    compile "io.vertx:vertx-unit:$version_vertx"
    compile "io.vertx:vertx-codegen:$version_vertx"
    compile "io.vertx:vertx-lang-groovy:$version_vertx"
    compile "io.vertx:vertx-lang-js:$version_vertx"
    compile "io.vertx:vertx-service-factory:$version_vertx"
    compile "io.vertx:vertx-hazelcast:$version_vertx"
    compile "io.vertx:vertx-service-proxy:$version_vertx"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile "com.amazonaws:aws-java-sdk-kinesis:$version_kinesis"

    compile "nl.komponents.kovenant:kovenant:$version_kovenant"

    annotationProcessor "io.vertx:vertx-codegen:$version_vertx"
    
    testCompile "junit:junit:$version_junit"
}

task cleanGenerated(type: Delete) {
    delete generatedSrcDir
}

compileJava {
    dependsOn cleanGenerated
    doFirst {
        mkdir generatedSrcDir
    }
    options.compilerArgs += [
            "-AoutputDirectory=$generatedSrcDir",
            "-processor", "io.vertx.codegen.CodeGenProcessor"
    ]
    classpath += configurations.annotationProcessor
}

task runNpmKinesalite(type: Exec) {
	commandLine 'npm', 'install', '-g', 'kinesalite'
}

task startKinesalite(type: ExecWait) {
    command 'kinesalite'
    ready 'Listening at http://0.0.0.0:4567'
    directory '..'
}

test {
    dependsOn runNpmKinesalite, startKinesalite
}

// https://fbflex.wordpress.com/2013/03/14/gradle-madness-execwait-a-task-that-waits-for-commandline-calls-to-be-ready/
class ExecWait extends DefaultTask {
    String command
    String ready
    String directory
 
    @TaskAction
    def spawnProcess() {
 
        ProcessBuilder builder = new ProcessBuilder(command.split(' '))
        builder.redirectErrorStream(true)
        builder.directory(new File(directory))
        Process process = builder.start()
 
        InputStream stdout = process.getInputStream()
        BufferedReader reader = new BufferedReader(new InputStreamReader(stdout))
 
        def line
        while ((line = reader.readLine()) != null) {
            println line
            if (line.contains(ready)) {
                println "$command is ready"
                break;
            }
        }
    }
}

task freeAllPorts << {
    def ports = [4567]
 
    ports.each { port ->
        def cmd = "lsof -Fp -i :$port"
        def process = cmd.execute()
        process.in.eachLine { line ->
            def killProcess = "kill -9 ${ line.substring(1) }".execute()
            killProcess.waitFor()
        }
    }
}

test.finalizedBy(freeAllPorts)

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "https://collokia.artifactoryonline.com/collokia/collokia-internal"
            credentials {
                username "${collokiaRepoUser}"
                password "${collokiaRepoPass}"
            }
        }
    }
}